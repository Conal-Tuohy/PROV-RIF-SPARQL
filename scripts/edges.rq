#    Copyright 2016 Conal Tuohy
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.

PREFIX crm: <http://erlangen-crm.org/current/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?SOURCE ?TARGET
WHERE {
	# links between Functions and Agencies
	{
		?agency a crm:E40_Legal_Body.
		?function a crm:E7_Activity.
		?function crm:P14_carried_out_by ?agency.
		
		?agency crm:P1_is_identified_by ?agency_identifier.
		?agency_identifier a crm:E42_Identifier.
		?agency_identifier rdf:value ?agency_identifier_value.
		
		?agency crm:P1_is_identified_by ?agency_appellation.
		?agency_appellation a crm:E82_Actor_Appellation.
		?agency_appellation rdf:value ?agency_appellation_value.
		
		OPTIONAL {
			?agency crm:P95i_was_formed_by ?agency_formation.
			?agency_formation crm:P4_has_time-span ?agency_formation_timespan.
			?agency_formation_timespan rdf:value ?agency_formation_date.
		}
		OPTIONAL {
			?agency crm:P99i_was_dissolved_by ?agency_dissolution.
			?agency_dissolution crm:P4_has_time-span ?agency_dissolution_timespan.
			?agency_dissolution_timespan rdf:value ?agency_dissolution_date.
		}	
		
		bind(
			concat(
				?agency_identifier_value, 
				' ', 
				?agency_appellation_value,
				COALESCE(
					concat(' ', xsd:string(?agency_formation_date), '–', xsd:string(?agency_dissolution_date)),
					concat(' ', xsd:string(?agency_formation_date), '–'),
					concat(' –', xsd:string(?agency_dissolution_date)),
				'' 			
				)
			) as ?TARGET
		).
	
		?function crm:P1_is_identified_by ?function_identifier.
		?function_identifier a crm:E42_Identifier.
		?function_identifier rdf:value ?function_identifier_value.	
		
		?function crm:P1_is_identified_by ?function_appellation.
		?function_appellation a crm:E41_Appellation.
		?function_appellation rdf:value ?function_appellation_value.
		
		bind(concat(?function_identifier_value, ' ', ?function_appellation_value) as ?SOURCE).
	}
UNION
	# links between Series and the Agencies which created them
	{
		# series labels consist of the series id and name and optional date rang
		?series a crm:E78_Collection.
		?series crm:P1_is_identified_by ?seriesName.
		?series crm:P1_is_identified_by ?seriesId.
		?seriesName a crm:E41_Appellation.
		?seriesId a crm:E42_Identifier.
		?seriesName rdf:value ?seriesNameValue.
		?seriesId rdf:value ?seriesIdValue.
		?series crm:P147_was_curated_by ?curation.

		# the date range of the series in custody may not be known
		optional {
			?series crm:P147_was_curated_by ?custodyCuration.
			?curation a crm:E87_Curation_Activity.
			optional {
				?custodyCuration crm:P116_is_started_by ?curationStart.
				?curationStart a crm:E5_Event.
				?curationStart crm:P4_has_time-span ?curationStartDate.
				?curationStartDate a crm:E52_Time-Span.
				?curationStartDate rdf:value ?curationStartDateValue
			}
			optional {
				?custodyCuration crm:P115_is_finished_by ?curationEnd.
				?curationEnd a crm:E5_Event.
				?curationEnd crm:P4_has_time-span ?curationEndDate.
				?curationEndDate a crm:E52_Time-Span.
				?curationEndDate rdf:value ?curationEndDateValue
			}
      		# other curation activities (whose start and end are both unknown) are of no interest
			filter(bound(?curationEnd) || bound(?curationStart))
		}			

		bind(
			concat(?seriesIdValue, ' ', ?seriesNameValue,
				coalesce(
						 concat(' ', xsd:string(?curationStartDateValue), '–', xsd:string(?curationEndDateValue)),
						 concat(' ', xsd:string(?curationStartDateValue), '–'),
						 concat(' –', xsd:string(?curationEndDateValue)),
						 '' 			
				  )
			)
		 as ?SOURCE)
		
		# agency names consist of the agency id, name, and optionally existence dates
			
		?curation crm:P14_carried_out_by ?agency.
		?agency a crm:E40_Legal_Body.
		?agency crm:P1_is_identified_by ?agency_identifier.
		?agency_identifier a crm:E42_Identifier.
		?agency_identifier rdf:value ?agency_identifier_value.
		
		?agency crm:P1_is_identified_by ?agency_appellation.
		?agency_appellation a crm:E82_Actor_Appellation.
		?agency_appellation rdf:value ?agency_appellation_value.
		
		OPTIONAL {
			?agency crm:P95i_was_formed_by ?agency_formation.
			?agency_formation crm:P4_has_time-span ?agency_formation_timespan.
			?agency_formation_timespan rdf:value ?agency_formation_date.
		}
		OPTIONAL {
			?agency crm:P99i_was_dissolved_by ?agency_dissolution.
			?agency_dissolution crm:P4_has_time-span ?agency_dissolution_timespan.
			?agency_dissolution_timespan rdf:value ?agency_dissolution_date.
		}	
		
		bind(
			concat(
				?agency_identifier_value, 
				' ', 
				?agency_appellation_value,
				COALESCE(
					concat(' ', xsd:string(?agency_formation_date), '–', xsd:string(?agency_dissolution_date)),
					concat(' ', xsd:string(?agency_formation_date), '–'),
					concat(' –', xsd:string(?agency_dissolution_date)),
				'' 			
				)
			) as ?TARGET
		).		
		
	}
}
