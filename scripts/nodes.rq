PREFIX crm: <http://erlangen-crm.org/current/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
select ?NAME ?PERIOD ?URL
where
{
  {
      ?function a crm:E7_Activity.
      ?function crm:P70_is_documented_in ?URL.
      ?function crm:P1_is_identified_by ?functionName.
      ?function crm:P1_is_identified_by ?functionId.
      ?functionName a crm:E41_Appellation.
      ?functionId a crm:E42_Identifier.
      ?functionName rdf:value ?functionNameValue.
      ?functionId rdf:value ?functionIdValue.
      # function start and end dates may be unknown
      optional {
      	?function crm:P116_is_started_by ?functionStart.
      	?functionStart a crm:E5_Event.
      	?functionStart crm:P4_has_time-span ?functionStartDate.
      	?functionStartDate a crm:E52_Time-Span.
      	?functionStartDate rdf:value ?functionStartDateValue.
      }
      optional {
      	?function crm:P115_is_finished_by ?functionEnd.
      	?functionEnd a crm:E5_Event.
      	?functionEnd crm:P4_has_time-span ?functionEndDate.
      	?functionEndDate a crm:E52_Time-Span.
      	?functionEndDate rdf:value ?functionEndDateValue.
      }
      bind(
		concat(
		  if(
			 bound(?functionStartDateValue),
			 xsd:string(?functionStartDateValue), 
			 ''
		  ),
		  '-', 
		  if(
			 bound(?functionEndDateValue), 
			 xsd:string(?functionEndDateValue),
			 ''
		  )
		)
	 as ?PERIOD).
      bind(concat(?functionIdValue, ' ', ?functionNameValue) as ?NAME)
  }
  UNION
  {
      ?series a crm:E78_Collection.
      ?series crm:P70_is_documented_in ?URL.
      ?series crm:P1_is_identified_by ?seriesName.
      ?series crm:P1_is_identified_by ?seriesId.
      ?seriesName a crm:E41_Appellation.
      ?seriesId a crm:E42_Identifier.
      ?seriesName rdf:value ?seriesNameValue.
      ?seriesId rdf:value ?seriesIdValue.
      # the date range of the series in custody may not be known
      optional {
      	?series crm:P147_was_curated_by ?curation.
      	?curation a crm:E87_Curation_Activity.
      	optional {
			?curation crm:P116_is_started_by ?curationStart.
			?curationStart a crm:E5_Event.
			?curationStart crm:P4_has_time-span ?curationStartDate.
			?curationStartDate a crm:E52_Time-Span.
			?curationStartDate rdf:value ?curationStartDateValue
      	}
      	optional {
			?curation crm:P115_is_finished_by ?curationEnd.
			?curationEnd a crm:E5_Event.
			?curationEnd crm:P4_has_time-span ?curationEndDate.
			?curationEndDate a crm:E52_Time-Span.
			?curationEndDate rdf:value ?curationEndDateValue
      	}
      	# other curation activities (whose start and end are both unknown) are of no interest
		filter(bound(?curationEnd) || bound(?curationStart))
      }
      bind(
		concat(
		  if(
			 bound(?curationStartDateValue),
			 xsd:string(?curationStartDateValue), 
			 ''
		  ),
		  '-', 
		  if(
			 bound(?curationEndDateValue), 
			 xsd:string(?curationEndDateValue),
			 ''
		  )
		)
	 as ?PERIOD).      
      bind(
      	concat(?seriesIdValue, ' ', ?seriesNameValue,
				coalesce(
						 concat(' ', xsd:string(?curationStartDateValue), '–', xsd:string(?curationEndDateValue)),
						 concat(' ', xsd:string(?curationStartDateValue), '–'),
						 concat(' –', xsd:string(?curationEndDateValue)),
						 '' 			
				  )
      	)
      as ?NAME) 
  }
  UNION
  {
    ?agency a crm:E40_Legal_Body.
    ?agency crm:P70_is_documented_in ?URL.
    ?agency crm:P1_is_identified_by ?agencyName.
    ?agency crm:P1_is_identified_by ?agencyId.
    ?agencyName a crm:E82_Actor_Appellation.
    ?agencyId a crm:E42_Identifier.
    ?agencyName rdf:value ?agencyNameValue.
    ?agencyId rdf:value ?agencyIdValue.

    #NB agency formation and dissolution events and dates may not be known
    optional {
      ?agency crm:P95i_was_formed_by ?agencyFormation.
      ?agencyFormation a crm:E66_Formation.
      ?agencyFormation crm:P4_has_time-span ?agencyFormationDate.
      ?agencyFormationDate a crm:E52_Time-Span.
      ?agencyFormationDate rdf:value ?agencyFormationDateValue.
    }
    optional {
      ?agency crm:P99i_was_dissolved_by ?agencyDissolution.
      ?agencyDissolution a crm:E68_Dissolution.
      ?agencyDissolution crm:P4_has_time-span ?agencyDissolutionDate.
      ?agencyDissolutionDate a crm:E52_Time-Span.
      ?agencyDissolutionDate rdf:value ?agencyDissolutionDateValue.
    }
    bind(
        concat(
            ?agencyIdValue, 
            ' ', 
            ?agencyNameValue,
            COALESCE(
                concat(' ', xsd:string(?agencyFormationDateValue), '–', xsd:string(?agencyDissolutionDateValue)),
                concat(' ', xsd:string(?agencyFormationDateValue), '–'),
                concat(' –', xsd:string(?agencyDissolutionDateValue)),
                '' 			
            )
        ) as ?NAME
    ). 
    bind(
      concat(
        if(
          bound(?agencyFormationDateValue),
          xsd:string(?agencyFormationDateValue), 
          ''
        ),
        '-', 
        if(
          bound(?agencyDissolutionDateValue), 
          xsd:string(?agencyDissolutionDateValue),
          ''
        )
      )
    as ?PERIOD).
  }  
}
